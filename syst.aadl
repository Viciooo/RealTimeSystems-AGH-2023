package syst
public
	with devices;
	with hardware;
	with software;

	system SecuritySystem
	end SecuritySystem;

	system implementation SecuritySystem.with_devices
		subcomponents
			this_motion_sensor: device devices::MotionSensor;
			this_magnetic_alarm_sensor: device devices::MagneticAlarmSensor;
			this_camera: device devices::Camera;
			this_alarm_unit: device devices::AlarmUnit;

			this_controller: process software::Controller.with_threads;

			this_bus: bus hardware::HWConnection.impl;

			this_processor: processor hardware::MainProcessor.impl;

		connections
			motion_conn: port this_motion_sensor.motion_detected -> this_controller.motion_in;
			magnetic_conn: port this_magnetic_alarm_sensor.alarm_set_off -> this_controller.magnetic_in;
			camera_conn_out: port this_controller.camera_out -> this_camera.camera_on_off;
			alarm_conn: port this_controller.alarm_out -> this_alarm_unit.alarm_on_off;
			camera_conn_in: port this_camera.video_stream -> this_controller.video_from_camera;

			bus_motion: bus access this_bus -> this_motion_sensor.bus_access;
			bus_magnetic: bus access this_bus -> this_magnetic_alarm_sensor.bus_access;
			bus_camera: bus access this_bus -> this_camera.bus_access;
			bus_alarm: bus access this_bus -> this_alarm_unit.bus_access;

			bus_processor: bus access this_bus -> this_processor.bus_access;

	end SecuritySystem.with_devices;

	
end syst;